/*
 * generated by Xtext
 */
package br.usp.icmc.amenu.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess
import br.usp.icmc.amenu.aMenu.Menu
import br.usp.icmc.amenu.aMenu.Item
import br.usp.icmc.amenu.aMenu.Icon

/**
 * Generates code from your model files on save.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#TutorialCodeGeneration
 */
class AMenuGenerator implements IGenerator {
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		var menu = resource.contents.head as Menu
		fsa.generateFile('menu.html', menu.genMenu)
	}
	
	def genMenu(Menu m)'''
		<nav role="navigation">
			<ul«IF m.name != null» id="«m.name»"«ENDIF» aria-hidden="false" role="menubar">
			«FOR i : m.items»
				«i.genItem»
			«ENDFOR»
			</ul>
		</nav>
	'''
	
	def genSubMenu(Menu m)'''
			<ul«IF m.name != null» id="«m.name»"«ENDIF» aria-hidden="true" role="menu">
			«FOR i : m.items»
				«i.genItem»
			«ENDFOR»
			</ul>
	'''
	
	def genItem(Item item)'''
		<li«IF item.name != null» id="«item.name»"«ENDIF» role="menuitem"«IF item.menu != null» aria-haspopup="true"«ENDIF»>
			<a href="«item.action»" title="«IF item.title != null»«item.title»«ELSE»«item.label»«ENDIF»">
				«IF item.icon != null»
					«item.icon.genIcon»
				«ENDIF»
				«item.label»
			</a>
				«IF item.menu != null»
					«item.menu.genSubMenu»
				«ENDIF»
		</li>
	'''
	
	def genIcon(Icon icon)'''
		<img «IF icon.name != null» id="«icon.name»" «ENDIF» src="«icon.address» alt="«icon.text_alt»" />
	'''
}
